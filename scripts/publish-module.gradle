apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        // For Android libraries
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        // For pure Kotlin libraries, in case you have them
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // The coordinates of the library, being set from variables that
                // we'll set up later
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                // Two artifacts, the `aar` (or `jar`) and the sources
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar
               // artifact javadocJar

                // Mostly self-explanatory metadata
                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'a helper library for everything Activity and Fragment navigation in android'
                    url = 'https://github.com/Yungjay131/navigator'

                    signing {
                        sign publishing.publications.release
                        sign configurations.archives
                    }

                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://github.com/Yungjay131/navigator/blob/master/LICENSE'
                        }
                    }

                    developers {
                        developer {
                            id = 'Yungjay131'
                            name = 'Joshua Sylvanus'
                            email = 'talk2joshuasylvanus@gmail.com'
                        }
                    }

                    // Version control info - if you're using GitHub, follow the
                    // format as seen here
                    scm {
                        connection = 'scm:git:github.com/Yungjay131/navigator.git'
                        developerConnection = 'scm:git:ssh://github.com/Yungjay131/navigator.git'
                        url = 'https://github.com/Yungjay131/navigator/tree/master'
                    }
                }
            }
        }

       /* repositories  {
            maven {
                url = uri("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/")
                credentials = MAVEN_CENTRAL_USERNAME
                password = MAVEN_CENTRAL_PASSWORD
                 credentials {
                    username = MAVEN_CENTRAL_USERNAME
                    password = MAVEN_CENTRAL_PASSWORD
                }
            }
        }*/
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )
    sign publishing.publications
}
